plugins {
	id 'maven-publish'
	id 'fabric-loom' version '1.6-SNAPSHOT' apply false

	// https://github.com/ReplayMod/preprocessor
	// https://github.com/Fallen-Breath/preprocessor
	id 'com.replaymod.preprocess' version 'ce1aeb2b'

	// https://github.com/Fallen-Breath/yamlang
	id 'me.fallenbreath.yamlang' version '1.4.0' apply false
}

preprocess {
	def mc1165 = createNode('1.16.5', 1_16_05, 'yarn')
	def mc1171 = createNode('1.17.1', 1_17_01, 'yarn')
	def mc1182 = createNode('1.18.2', 1_18_02, 'yarn')
	def mc1194 = createNode('1.19.4', 1_19_04, 'yarn')
	def mc1206 = createNode('1.20.6', 1_20_06, 'yarn')
	def mc1210 = createNode('1.21'  , 1_21_00, 'yarn')

	mc1165.link(mc1171, null)
	mc1171.link(mc1182, null)
	mc1182.link(mc1194, null)
	mc1194.link(mc1206, null)
	mc1206.link(mc1210, null)
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.tasks.named('build').get()
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		subprojects {
			copy {
				from(buildLibs(project)) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar', '*-shadow.jar'
				}
				into buildLibs(rootProject)
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
}
